#!/usr/bin/perl
# Script for formatting documentation from definition lists
# (they get out of extract-doc.pl as a side-product).
# (c) 2008 Michal Vaner <vorner@ucw.cz>
use strict;
use warnings;

my $head = shift;
my $out = shift;

open OUT, ">$out" or die "Could not write output $out ($!)\n";
open HEAD, $head or die "Could not open head $head ($!)\n";
print OUT foreach( <HEAD> );
close HEAD;

my $dir = $out;
$dir =~ s/\/[^\/]+$//;

my @dump;

while( defined( my $line = <> ) ) {
	chomp $line;
	push @dump, [ split /,/, $line, 5 ];
}

my %groups = (
	'enum' => 1,
	'struct' => 1,
	'function' => 2,
	'variable' => 3,
	'define' => 4
);

foreach( sort { ( $groups{$a->[2]} <=> $groups{$b->[2]} ) or ( $a->[3] cmp $b->[3] ); } @dump ) {
	my( $file, $num, $type, $name, $text ) = @{$_};
	my $dircp = $dir;
	while( shift @{[ $dircp =~ /([^\/]+)/, "//" ]} eq shift @{[ $file =~ /([^\/]+)/, "///" ]} ) {
		$dircp =~ s/[^\/]+\/?//;
		$file =~ s/[^\/]+\/?//;
	}
	$dircp =~ s/[^\/]+/../g;
	$file = $dircp."/".$file;
	$file =~ s/^\///;
	$file =~ s/\.[^.]+$//;
	$text =~ s/\(/!!PARENT_OPEN!!/g;
	print OUT "- <<$file:auto_$num,`$text`>>\n";
}

close OUT;
